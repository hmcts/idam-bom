plugins {
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.5'
}

Node bom = new XmlParser(false, false).parse(file('pom.xml'))
group = bom.groupId.text()
version = bom.version.text()

publishing {
  publications {
    'idam-bom'(MavenPublication) {
      pom.withXml {
        def root = asNode()

        bom.each { tag ->
          def name = "{http://maven.apache.org/POM/4.0.0}${tag.name()}"
          def node = root.children().find { it.name().toString() == name }

          if (node != null) {
            // Replace any tags gradle's default pom contains with those of our bom
            println "Replacing tag: ${node.name()}"
            node.replaceNode(tag)
          } else {
            // Add additional tags from our bom
            println "Adding tag: ${tag.name()}"
            root.append(tag)
          }
        }
      }
    }
  }
}

bintray {
  user = System.getenv('BINTRAY_USER')
  key = System.getenv('BINTRAY_KEY')
  publications = ['idam-bom']
  publish = true
  pkg {
    repo = 'hmcts-maven'
    name = "${rootProject.name}"
    userOrg = 'hmcts'
    licenses = ['MIT']
    vcsUrl = "https://github.com/hmcts/${rootProject.name}"
    version {
      name = project.version
    }
  }
}

